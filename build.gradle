buildscript {
    ext {
        dropwizardVersion = '2.0.2'
        lombokVersion = '1.18.12'
    }
}

plugins {
    id 'java'
    id 'application'
    id 'jacoco'
    id 'checkstyle'
    id "com.github.spotbugs" version "4.0.4"
    id "com.github.kt3k.coveralls" version "2.10.2"
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
}

repositories {
    mavenCentral()
}

sourceSets {
    componentTest {
        java.srcDir file("src/component-test/java")
        resources.srcDir file("src/component-test/resources")
    }
}

task componentTest(type: Test)

tasks {
    wrapper {
        gradleVersion = "5.6.4"
    }

    application {
        mainClassName = 'lt.rieske.skeleton.SkeletonService'
    }

    jacocoTestReport {
        reports {
            xml.enabled = true
            html.enabled = true
        }
    }

    checkstyle {
        configFile = new File(rootDir, "checkstyle.xml")
    }

    test {
        useJUnitPlatform()
    }

    componentTest {
        dependsOn(test)
        testClassesDirs = sourceSets.componentTest.output.classesDirs
        classpath = sourceSets.componentTest.runtimeClasspath
        testLogging.showStandardStreams = true
        testLogging.exceptionFormat = "full"
        exclude("**/**Test.class")
    }

    check.dependsOn(componentTest)

    run {
        args 'server', './configuration/production.yml'
    }

    run.dependsOn check
}

dependencies {
    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"

    implementation "io.dropwizard:dropwizard-core:${dropwizardVersion}"
    implementation "io.dropwizard:dropwizard-client:${dropwizardVersion}"

    implementation("javax.xml.bind:jaxb-api:2.3.1")
    implementation("javax.activation:activation:1.1.1")

    testImplementation("org.junit.jupiter:junit-jupiter-api:5.5.2")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:5.6.0")
    testImplementation "io.dropwizard:dropwizard-testing:${dropwizardVersion}"

    componentTestImplementation(configurations.implementation)
    componentTestImplementation("org.testcontainers:testcontainers:1.12.4")
    componentTestImplementation("io.rest-assured:rest-assured:4.3.0")
}

