buildscript {
    ext {
        dropwizardVersion = '1.3.8'
        lombokVersion = '1.18.10'
    }
    repositories {
        mavenCentral()
    }
}

plugins {
    id 'java'
    id 'application'
    id 'jacoco'
    id 'checkstyle'
    id "com.github.spotbugs" version "2.0.1"
    id "com.github.kt3k.coveralls" version "2.8.2"
}

repositories {
    mavenCentral()
}

project.group = 'lt.rieske.skeleton'
project.archivesBaseName = 'dropwizard-skeleton'
project.version = "DEV-SNAPSHOT"

sourceCompatibility = 11

mainClassName = 'lt.rieske.skeleton.SkeletonService'

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

checkstyle {
    configFile = new File(rootDir, "checkstyle.xml")
}

jar.dependsOn check

dependencies {
    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"

    compile "io.dropwizard:dropwizard-core:${dropwizardVersion}"
    compile "io.dropwizard:dropwizard-client:${dropwizardVersion}"

    testCompile "junit:junit:4.12"
    testCompile "org.assertj:assertj-core:3.14.0"
    testCompile "io.dropwizard:dropwizard-testing:${dropwizardVersion}"
    testCompile "org.mockito:mockito-core:3.1.0"
}

jar {
    def manifestClasspath = configurations.compile.collect { "lib/" + it.getName() }.join(' ')
    manifest {
        attributes 'Build-Date': new Date(),
                'Build-JDK': System.getProperty('java.version'),
                'Main-Class': "$mainClassName",
                'Class-Path': manifestClasspath
    }
}

task copyDependenciesToDistribution(type: Copy) {
    from configurations.runtime
    into "build/distributions/lib"
}

assemble {
    dependsOn copyDependenciesToDistribution
}

run {
    args 'server', './configuration/production.yml'
}

run.dependsOn check

wrapper {
    gradleVersion = "5.6.4"
}
